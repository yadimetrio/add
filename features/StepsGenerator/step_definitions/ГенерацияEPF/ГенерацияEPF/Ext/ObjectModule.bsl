Перем Ванесса;

Функция ПолучитьСписокТестов(КонтекстФреймворкаBDD) Экспорт
	Ванесса = КонтекстФреймворкаBDD;

	ВсеТесты = Новый Массив;

	// описание шагов
	//пример вызова Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,Снипет,ИмяПроцедуры,ПредставлениеТеста,Транзакция,Параметр);

	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ЯПерешелНаЗакладкуГенераторEPF()","ЯПерешелНаЗакладкуГенераторEPF","И я перешел на закладку генератор EPF");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ЕслиОжидаемыйФайлEpfУжеСуществуетТоОнБудетУдален()","ЕслиОжидаемыйФайлEpfУжеСуществуетТоОнБудетУдален","И если ожидаемый файл epf уже существует, то он будет удален");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ЯНажалНаКнопку(Парам01)","ЯНажалНаКнопку","И я нажал на кнопку ""СоздатьШаблоныОбработок""");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ЯПолучилСгенерированныйEpfФайлВОжидаемомКаталоге()","ЯПолучилСгенерированныйEpfФайлВОжидаемомКаталоге","Тогда я получил сгенерированный epf файл в ожидаемом каталоге");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"СгенерированныйEpfПрошелПроверкуНаКорректность()","СгенерированныйEpfПрошелПроверкуНаКорректность","И сгенерированный epf прошел проверку на корректность");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"СгенерированныйEpfЗнакМинусВСценарииПрошелПроверкуНаКорректность()","СгенерированныйEpfЗнакМинусВСценарииПрошелПроверкуНаКорректность","И сгенерированный epf ЗнакМинусВСценарии прошел проверку на корректность");

	Возврат ВсеТесты;
КонецФункции

Процедура ПередНачаломСценария() Экспорт
КонецПроцедуры

Процедура ПередОкончаниемСценария() Экспорт
	ИмяОжидаемогоФайла = Контекст.ИмяОжидаемогоФайла;
	Файл = Новый Файл(ИмяОжидаемогоФайла);
	Если Файл.Существует() Тогда
		Ванесса.ПолучитьФорму("Форма").СделатьСообщение("ПередОкончаниемСценария(). Удаляю файл " + ИмяОжидаемогоФайла);
		УдалитьФайлы(ИмяОжидаемогоФайла);
	КонецЕсли;

	Если Контекст.Свойство("ОткрытаяФормаVanessaADD") Тогда
		Если Контекст.ОткрытаяФормаVanessaADD.Открыта() Тогда
			Контекст.ОткрытаяФормаVanessaADD.Закрыть();
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры


//я перешел на закладку генератор EPF
//@ЯПерешелНаЗакладкуГенераторEPF()
Процедура ЯПерешелНаЗакладкуГенераторEPF() Экспорт
	ОткрытаяФормаVanessaADD = Контекст.ОткрытаяФормаVanessaADD;
	ОткрытаяФормаVanessaADD.ЭлементыФормы.пЗапускСценариев.ТекущаяСтраница = ОткрытаяФормаVanessaADD.ЭлементыФормы.пЗапускСценариев.Страницы.СтраницаГенераторEPF;
КонецПроцедуры

//ожидаемый файл epf уже существует, то он будет удален
//@ЕслиОжидаемыйФайлEpfУжеСуществуетТоОнБудетУдален()
Процедура ЕслиОжидаемыйФайлEpfУжеСуществуетТоОнБудетУдален() Экспорт
	ИмяОжидаемогоФайла = Контекст.ОткрытаяФормаVanessaADD.КаталогИнструментов + "\features\Support\Templates\step_definitions\" + Контекст.ИмяТестовойФичи + ".epf";
	Контекст.Вставить("ИмяОжидаемогоФайла",ИмяОжидаемогоФайла);

	ИмяОжидаемогоФайла = Контекст.ИмяОжидаемогоФайла;
	Файл = Новый Файл(ИмяОжидаемогоФайла);
	Если Файл.Существует() Тогда
		Сообщить("Удаляю файл " + ИмяОжидаемогоФайла);
		УдалитьФайлы(ИмяОжидаемогоФайла);
	КонецЕсли;

	Контекст.Вставить("ИмяОжидаемогоФайла",ИмяОжидаемогоФайла);

КонецПроцедуры


//я нажал на кнопку "СоздатьШаблоныОбработок"
//@ЯНажалНаКнопку(Парам01Строка)
Процедура ЯНажалНаКнопку(ИмяКнопки) Экспорт
	ОткрытаяФормаVanessaADD = Контекст.ОткрытаяФормаVanessaADD;

	Если ИмяКнопки = "СоздатьШаблоныОбработок" Тогда
		ОткрытаяФормаVanessaADD.СоздатьШаблоныОбработок();
	Иначе
		ВызватьИсключение "Не смог обработать нажатие кнопки: " + ИмяКнопки;
	КонецЕсли;

КонецПроцедуры

//я получил сгенерированный epf файл в ожидаемом каталоге
//@ЯПолучилСгенерированныйEpfФайлВОжидаемомКаталоге()
Процедура ЯПолучилСгенерированныйEpfФайлВОжидаемомКаталоге() Экспорт
	ИмяОжидаемогоФайла = Контекст.ИмяОжидаемогоФайла;
	Файл = Новый Файл(ИмяОжидаемогоФайла);
	Ванесса.ПроверитьРавенство(Файл.Существует(),Истина,"Был создан epf в нужном каталоге");

КонецПроцедуры

//сгенерированный epf прошел проверку на корректность
//@СгенерированныйEpfПрошелПроверкуНаКорректность()
Процедура СгенерированныйEpfПрошелПроверкуНаКорректность() Экспорт
	ИмяОжидаемогоФайла = Контекст.ИмяОжидаемогоФайла;
	СозданнаяОбработка = ВнешниеОбработки.Создать(ИмяОжидаемогоФайла);

	//должна быть эта процедура, и она должна быть без параметров
	Попытка
		СозданнаяОбработка.ФичаСодержитСтрокуСТочкойВКонце();
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();
		Если Найти(ОписаниеОшибки,"Не реализовано.") = 0 Тогда
			ВызватьИсключение "Не верно сгенерировалась epf! ФичаСодержитСтрокуСТочкойВКонце." + Символы.ПС + ОписаниеОшибки;
		КонецЕсли;
	КонецПопытки;

	Попытка
		СозданнаяОбработка.ФичаСодержитЧислоИЗапятуюПослеНего(10);
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();
		Если Найти(ОписаниеОшибки,"Не реализовано.") = 0 Тогда
			ВызватьИсключение "Не верно сгенерировалась epf! ФичаСодержитЧислоИЗапятуюПослеНего()." + Символы.ПС + ОписаниеОшибки;
		КонецЕсли;
	КонецПопытки;

	Попытка
		СозданнаяОбработка.УстанавливаетсяПустойМок_ВСистемуТестирования();
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();
		Если Найти(ОписаниеОшибки,"Не реализовано.") = 0 Тогда
			ВызватьИсключение "Не верно сгенерировалась epf! УстанавливаетсяПустойМок_ВСистемуТестирования()" + Символы.ПС + ОписаниеОшибки;
		КонецЕсли;
	КонецПопытки;

КонецПроцедуры

//И сгенерированный epf ЗнакМинусВСценарии прошел проверку на корректность
//@СгенерированныйEpfЗнакМинусВСценарииПрошелПроверкуНаКорректность()
Процедура СгенерированныйEpfЗнакМинусВСценарииПрошелПроверкуНаКорректность() Экспорт
	ИмяОжидаемогоФайла = Контекст.ИмяОжидаемогоФайла;
	СозданнаяОбработка = ВнешниеОбработки.Создать(ИмяОжидаемогоФайла);
КонецПроцедуры
