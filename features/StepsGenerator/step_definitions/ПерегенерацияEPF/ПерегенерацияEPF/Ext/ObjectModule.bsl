Перем Ванесса;

Функция ПолучитьСписокТестов(КонтекстФреймворкаBDD) Экспорт
	Ванесса = КонтекстФреймворкаBDD;

	ВсеТесты = Новый Массив;

	// описание шагов
	//пример вызова Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,Снипет,ИмяПроцедуры,ПредставлениеТеста,Транзакция,Параметр);

	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ОжидаемыйФайлEpfУжеСуществует()","ОжидаемыйФайлEpfУжеСуществует","ожидаемый файл epf уже существует");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ВОжидаемомФайлеУжеЕстьРеализованныеШаги()","ВОжидаемомФайлеУжеЕстьРеализованныеШаги","в ожидаемом файле уже есть реализованные шаги");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ВФичеЕстьШагиКоторыхНетВОжидаемомEpfФайле()","ВФичеЕстьШагиКоторыхНетВОжидаемомEpfФайле","в фиче есть шаги, которых нет в ожидаемом epf файле");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"СгенерированныйEpfПрошелПроверкуНаКорректностьПослеПерегенерации()","СгенерированныйEpfПрошелПроверкуНаКорректностьПослеПерегенерации","сгенерированный epf прошел проверку на корректность после перегенерации");

	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ОчищеныВременныеФайлыРезультатовПрошлыхГенераций()","ОчищеныВременныеФайлыРезультатовПрошлыхГенераций","Дано Очищены временные файлы результатов прошлых генераций");

	Возврат ВсеТесты;
КонецФункции

Процедура ПередНачаломСценария() Экспорт
	ИмяОжидаемогоФайла = Ванесса.КаталогИнструментов + "\features\Support\Templates\step_definitions\ФичаДляПроверкиПерегенерацииEPF.epf";
	ФайлПроверкаСуществования = Новый Файл(ИмяОжидаемогоФайла);
	Если ФайлПроверкаСуществования.Существует() Тогда
		УдалитьФайлы(ИмяОжидаемогоФайла);
	КонецЕсли;
	Контекст.Вставить("ИмяОжидаемогоФайла",ИмяОжидаемогоФайла);
КонецПроцедуры

Процедура ПередОкончаниемСценария() Экспорт
	ИмяОжидаемогоФайла = Контекст.ИмяОжидаемогоФайла;
	ФайлПроверкаСуществования = Новый Файл(ИмяОжидаемогоФайла);
	Если ФайлПроверкаСуществования.Существует() Тогда
		УдалитьФайлы(ИмяОжидаемогоФайла);
	КонецЕсли;

	//безусловное закрытие формы если она осталась
	Попытка
	    ОткрытаяФормаVanessaADD = Контекст.ОткрытаяФормаVanessaADD;
		ОткрытаяФормаVanessaADD.Закрыть();
	Исключение

	КонецПопытки;

КонецПроцедуры

//ожидаемый файл epf уже существует
//@ОжидаемыйФайлEpfУжеСуществует()
Процедура ОжидаемыйФайлEpfУжеСуществует() Экспорт
	ИмяОжидаемогоФайла = Контекст.ИмяОжидаемогоФайла;
	ФайлПроверкаСуществования = Новый Файл(ИмяОжидаемогоФайла);
	Если ФайлПроверкаСуществования.Существует() Тогда
		УдалитьФайлы(ИмяОжидаемогоФайла);
	КонецЕсли;

	Ванесса.ПроверитьНеРавенство(ФайлПроверкаСуществования.Существует(),Истина,"В этот момент Файл " + ИмяОжидаемогоФайла + " НЕ должен существовать.");


	Макет = ПолучитьМакет("Макет");
	Макет.Записать(ИмяОжидаемогоФайла);

	ФайлПроверкаСуществования = Новый Файл(ИмяОжидаемогоФайла);
	Ванесса.ПроверитьРавенство(ФайлПроверкаСуществования.Существует(),Истина,"Файл " + ИмяОжидаемогоФайла + " должен существовать.");
КонецПроцедуры

//в ожидаемом файле уже есть реализованные шаги
//@ВОжидаемомФайлеУжеЕстьРеализованныеШаги()
Процедура ВОжидаемомФайлеУжеЕстьРеализованныеШаги() Экспорт
	ОбработкаДоПерегенерации = ВнешниеОбработки.Создать(Контекст.ИмяОжидаемогоФайла);
	ОбработкаДоПерегенерации.ЯМогуЭтотШагВыполнить();

	Контекст.Вставить("ОбработкаДоПерегенерации",ОбработкаДоПерегенерации);
КонецПроцедуры

//в фиче есть шаги, которых нет в ожидаемом epf файле
//@ВФичеЕстьШагиКоторыхНетВОжидаемомEpfФайле()
Процедура ВФичеЕстьШагиКоторыхНетВОжидаемомEpfФайле() Экспорт
	ОбработкаДоПерегенерации = Контекст.ОбработкаДоПерегенерации;

	МетодЕсть = Ложь;
	Попытка
		ОбработкаДоПерегенерации.ВEpfПоявилсяШагКоторогоНеБылоРаньше();
		МетодЕсть = Истина;
	Исключение

	КонецПопытки;

	Ванесса.ПроверитьРавенство(МетодЕсть,Ложь,"Метода ВEpfПоявилсяШагКоторогоНеБылоРаньше в epf не должно быть.");
КонецПроцедуры

//сгенерированный epf прошел проверку на корректность после перегенерации
//@СгенерированныйEpfПрошелПроверкуНаКорректностьПослеПерегенерации()
Процедура СгенерированныйEpfПрошелПроверкуНаКорректностьПослеПерегенерации() Экспорт

	ОбработкаПослеПерегенерации = ВнешниеОбработки.Создать(Контекст.ИмяОжидаемогоФайла);

	Нашли2 = Ложь;
	//в списке тестов должно быть снипеты УМеняЕстьПростойШаг и ЕстьПростойШаг
	СписокТестов = ОбработкаПослеПерегенерации.ПолучитьСписокТестов(Ванесса);
	Для Каждого Снипет Из СписокТестов Цикл
		Если Снипет.Снипет = "ЕстьПростойШаг()" Тогда
			Нашли2 = Истина;
		КонецЕсли;
	КонецЦикла;

	Если Не Нашли2 Тогда
		ВызватьИсключение "В списке шагов не нашли ЕстьПростойШаг()!";
	КонецЕсли;



	//этот шаг уже был и должен остаться
	Попытка
		ОбработкаПослеПерегенерации.УМеняЕстьПростойШаг();
	Исключение
		ВызватьИсключение "Исчез шаг УМеняЕстьПростойШаг()!";
	КонецПопытки;


	//должен был появиться этот шаг
	Попытка
		ОбработкаПослеПерегенерации.ЕстьПростойШаг();
	Исключение
		Стр = ОписаниеОшибки();
		Если Найти(Стр,"Не реализовано.") = 0 Тогда
			ВызватьИсключение "Должен был появиться метод ЕстьПростойШаг, но не появился.";
		КонецЕсли;
	КонецПопытки;


	//должен был появиться этот шаг
	Попытка
		ОбработкаПослеПерегенерации.ВEpfПоявилсяШагКоторогоНеБылоРаньше();
	Исключение
		Стр = ОписаниеОшибки();
		Если Найти(Стр,"Не реализовано.") = 0 Тогда
			ВызватьИсключение "Должен был появиться метод ВEpfПоявилсяШагКоторогоНеБылоРаньше, но не появился.";
		КонецЕсли;
	КонецПопытки;
КонецПроцедуры


//Дано Очищены временные файлы результатов прошлых генераций
//@ОчищеныВременныеФайлыРезультатовПрошлыхГенераций()
Процедура ОчищеныВременныеФайлыРезультатовПрошлыхГенераций() Экспорт

КонецПроцедуры
