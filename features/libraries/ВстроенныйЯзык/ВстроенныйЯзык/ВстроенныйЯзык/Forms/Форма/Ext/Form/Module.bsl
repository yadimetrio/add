//начало текста модуля

#Область Служебные_функции_и_процедуры

&НаКлиенте
Перем Ванесса;

&НаКлиенте
// Структура, в которой хранится состояние сценария между выполнением шагов. Очищается перед выполнением каждого сценария.
Перем Контекст Экспорт;

&НаКлиенте
// Структура, в которой можно хранить служебные данные между запусками сценариев. Существует, пока открыта форма Vanessa-ADD.
Перем КонтекстСохраняемый Экспорт;

&НаКлиенте
Функция ПолучитьСписокТестов(КонтекстФреймворкаBDD) Экспорт
	Ванесса = КонтекстФреймворкаBDD;

	ВсеТесты = Новый Массив;

	//описание параметров
	//пример вызова Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,Снипет,ИмяПроцедуры,ПредставлениеТеста,ОписаниеШага,ТипШага,Транзакция,Параметр);

	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ВыражениеВнутреннегоЯзыкаИстинно(Парам01)",
		"ВыражениеВнутреннегоЯзыкаИстинно",
		"И выражение внутреннего языка ""Контекст.КоличествоФинальное - Контекст.КоличествоНачальное = 1"" Истинно",
		"Позволяет проверить произвольное выражение встроенного языка","Встроенный язык");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ЯЗапоминаюЗначениеВыраженияВПеременную(Парам01,Парам02)",
		"ЯЗапоминаюЗначениеВыраженияВПеременную",
		"И Я запоминаю значение выражения ""Ванесса.ПолучитьМассивPIDОкон1С().Количество()"" в переменную ""КоличествоНачальное""",
		"Позволяет сохранить в переменную Контекст произвольное выражение встроенного языка","Встроенный язык");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ЯЗапоминаюЗначениеВыраженияВПеременнуюГлобально(Парам01,Парам02)",
		"ЯЗапоминаюЗначениеВыраженияВПеременнуюГлобально",
		"И Я запоминаю значение выражения ""Ванесса.ПолучитьМассивPIDОкон1С().Количество()"" в переменную ""КоличествоНачальное"" глобально",
		"Позволяет сохранить в переменную КонтекстСохраняемый произвольное выражение встроенного языка","Встроенный язык");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ЗатемЯВыполняюКодВстроенногоЯзыка(ТабПарам)",
		"ЗатемЯВыполняюКодВстроенногоЯзыка",
		"И затем я выполняю код встроенного языка",
		"Позволяет выполнить произвольный код 1С на клиенте. Возможно использование таблицы или многострочного текста","Встроенный язык");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ЯВыполняюКодВстроенногоЯзыка(ТабПарам)",
		"ЯВыполняюКодВстроенногоЯзыка",
		"И я выполняю код встроенного языка",
		"Позволяет выполнить произвольный код 1С на клиенте. Возможно использование таблицы или многострочного текста","Встроенный язык");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ЯВыполняюКодВстроенногоЯзыкаНаСервере(ТабПарам)",
		"ЯВыполняюКодВстроенногоЯзыкаНаСервере",
		"И я выполняю код встроенного языка на сервере",
		"Позволяет выполнить произвольный код 1С на сервере. Возможно использование таблицы или многострочного текста","Встроенный язык");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ЯПомещаюВПеременнуюРезультатВыполненияКодаВстроенногоЯзыкаНаСервере(Парам01,ТабПарам)",
		"ЯПомещаюВПеременнуюРезультатВыполненияКодаВстроенногоЯзыкаНаСервере",
		"И Я помещаю в переменную ""ИмяПеременной"" результат выполнения кода встроенного языка на сервере",
		"Позволяет выполнить произвольный код 1С на сервере и получить результат в переменную контекста. При выполнении кода на сервере результат выполнения можно сохранить в переменую ""Результат"", из которого и будет получено значение в переменную контекста. Возможно использование таблицы или многострочного текста","Встроенный язык");

	Возврат ВсеТесты;
КонецФункции

&НаКлиенте
Процедура ПередНачаломСценария() Экспорт
КонецПроцедуры

&НаКлиенте
Процедура ПередОкончаниемСценария() Экспорт
КонецПроцедуры

#КонецОбласти

#Область Шаги

&НаКлиенте
//И выражение внутреннего языка "Контекст.КоличествоНачальноеПлюсОдин - Контекст.КоличествоНачальное = 1" Истинно
//@ВыражениеВнутреннегоЯзыкаИстинно(Парам01)
Процедура ВыражениеВнутреннегоЯзыкаИстинно(Знач Выражение) Экспорт
	ЗначениеВыражения = Ванесса.ВычислитьВнутреннееВыражение(Выражение);
	Если ЗначениеВыражения <> Истина Тогда
		ВызватьИсключение "Выражение <" + Выражение + "> оказалось равно <" + ЗначениеВыражения + ">, а ожидали <" + Строка(Истина) + ">.";
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
//И Я запоминаю выражение внутреннего языка "Ванесса.ПолучитьМассивPIDОкон1С().Количество()" в переменную "КоличествоНачальноеПлюсОдинВторойРаз"
//@ЯЗапоминаюЗначениеВыраженияВПеременную(Парам01,Парам02)
Процедура ЯЗапоминаюЗначениеВыраженияВПеременную(Знач Выражение, Знач ИмяПеременной) Экспорт
	ЗначениеВыражения = Ванесса.ВычислитьВнутреннееВыражение(Выражение);
	СохранитьВКонтекст(ИмяПеременной, ЗначениеВыражения);
КонецПроцедуры

&НаКлиенте
//И Я запоминаю выражение внутреннего языка "Ванесса.ПолучитьМассивPIDОкон1С().Количество()" в переменную "КоличествоНачальноеПлюсОдинВторойРаз" глобально
//@ЯЗапоминаюЗначениеВыраженияВПеременнуюГлобально(Парам01,Парам02)
Процедура ЯЗапоминаюЗначениеВыраженияВПеременнуюГлобально(Знач Выражение, Знач ИмяПеременной) Экспорт
	ЗначениеВыражения = Ванесса.ВычислитьВнутреннееВыражение(Выражение);
	Попытка
		КонтекстСохраняемый.Вставить(ИмяПеременной, ЗначениеВыражения);
	Исключение
		Ванесса.СохранитьЗначениеВКонтекстСохраняемый(ИмяПеременной, ЗначениеВыражения);
	КонецПопытки;
КонецПроцедуры

&НаКлиенте
//И затем я выполняю код встроенного языка
//@ЗатемЯВыполняюКодВстроенногоЯзыка(ТабПарам)
Процедура ЗатемЯВыполняюКодВстроенногоЯзыка(Знач ТабПарамИлиМногострочнаяСтрока) Экспорт
	ЯВыполняюКодВстроенногоЯзыка(ТабПарамИлиМногострочнаяСтрока);
КонецПроцедуры

&НаКлиенте
//И я выполняю код встроенного языка
//@ЯВыполняюКодВстроенногоЯзыка(ТабПарам)
Процедура ЯВыполняюКодВстроенногоЯзыка(Знач ТабПарамИлиМногострочнаяСтрока) Экспорт
	Текст = ПолучитьТекстИзПараметра(ТабПарамИлиМногострочнаяСтрока);
	Выполнить(Текст);
КонецПроцедуры

&НаКлиенте
//И я выполняю код встроенного языка на сервере
//@ЯВыполняюКодВстроенногоЯзыкаНаСервере(ТабПарам)
Процедура ЯВыполняюКодВстроенногоЯзыкаНаСервере(Знач ТабПарамИлиМногострочнаяСтрока) Экспорт
	Текст = ПолучитьТекстИзПараметра(ТабПарамИлиМногострочнаяСтрока);
	ВыполнитьНаСервере(Текст);
КонецПроцедуры

&НаКлиенте
//И я выполняю код встроенного языка на сервере
//@ЯПомещаюВПеременнуюРезультатВыполненияКодаВстроенногоЯзыкаНаСервере(Парам01,ТабПарам)
Процедура ЯПомещаюВПеременнуюРезультатВыполненияКодаВстроенногоЯзыкаНаСервере(Знач ИмяПеременной, Знач ТабПарамИлиМногострочнаяСтрока) Экспорт
	Текст = ПолучитьТекстИзПараметра(ТабПарамИлиМногострочнаяСтрока);
	Результат = ВыполнитьНаСервере(Текст);
	СохранитьВКонтекст(ИмяПеременной, Результат);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПолучитьТекстИзПараметра(Знач ТабПарамИлиМногострочнаяСтрока)
	Текст = "";
	Если ТипЗнч(ТабПарамИлиМногострочнаяСтрока) = Тип("Строка") Тогда
		Текст = ТабПарамИлиМногострочнаяСтрока;
	Иначе
		ТабПарам = ТабПарамИлиМногострочнаяСтрока;
		Для Каждого СтрокаТабПарам Из ТабПарам Цикл
			Текст = Текст + СтрокаТабПарам.Кол1 + Символы.ПС;
		КонецЦикла;
	КонецЕсли;
	Возврат Текст;
КонецФункции

&НаСервереБезКонтекста
Функция ВыполнитьНаСервере(Текст)
	Результат = Неопределено;
	Выполнить(Текст);
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура СохранитьВКонтекст(ИмяПеременной, ЗначениеВыражения)
	Попытка
		Контекст.Вставить(ИмяПеременной, ЗначениеВыражения);
	Исключение
		Ванесса.СохранитьЗначениеВКонтекст(ИмяПеременной, ЗначениеВыражения);
	КонецПопытки;
КонецПроцедуры

#КонецОбласти
