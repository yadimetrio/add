#Область ОписаниеПеременных

Перем КонтекстЯдра;
Перем Утверждения;

#КонецОбласти

#Область ПрограммныйИнтерфейс

#Область ИнтерфейсТестирования

Функция КлючНастройки() Экспорт
	Возврат "МакетыСКД";
КонецФункции

Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	КонтекстЯдра = КонтекстЯдраПараметр;
	Утверждения = КонтекстЯдра.Плагин("БазовыеУтверждения");
	
	ЗагрузитьНастройки();
КонецПроцедуры

Процедура ЗаполнитьНаборТестов(НаборТестов, КонтекстЯдраПараметр) Экспорт
	
	КонтекстЯдра = КонтекстЯдраПараметр;
	
	ЗагрузитьНастройки();
	
	Если Не НужноВыполнятьТест() Тогда
		Возврат;
	КонецЕсли;

	ДобавитьОбщиеМакеты(НаборТестов);
	
	ДобавитьМакетМетаданных(НаборТестов);	
	
КонецПроцедуры

#КонецОбласти

#Область Тесты

Процедура ПередЗапускомТеста() Экспорт
	
КонецПроцедуры

Процедура ПослеЗапускаТеста() Экспорт

КонецПроцедуры

Процедура ТестДолжен_ПроверитьМакетСКД(ИмяМенеджера, ИмяОбьекта, ИмяМакета) Экспорт
	
	Менеджер = МенеджерОбьектаПоИмени(ИмяМенеджера);
	
	СхемаКомпоновкиДанных = Менеджер[ИмяОбьекта].ПолучитьМакет(СокрЛП(ИмяМакета));
	
	ПроверитьСхемуСКД(СхемаКомпоновкиДанных);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьОбщийМакетСКД(ИмяМакета) Экспорт
	
	СхемаКомпоновкиДанных = ПолучитьОбщийМакет(ИмяМакета);
	
	ПроверитьСхемуСКД(СхемаКомпоновкиДанных);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьВложенныйМакетСКД(ИмяМенеджера, ИмяОбьекта, ИменаМакетов) Экспорт
	
	ИменаМакетов = КонтекстЯдра.РазложитьСтрокуВМассивПодстрок(ИменаМакетов, "->");
	
	Если ИмяОбьекта = ТекстОбщиеМакеты() Тогда
		СхемаКомпоновкиДанных = ПолучитьОбщийМакет(ИменаМакетов[0]);
	Иначе
		Менеджер = МенеджерОбьектаПоИмени(ИмяМенеджера);
		СхемаКомпоновкиДанных = Менеджер[ИмяОбьекта].ПолучитьМакет(ИменаМакетов[0]);
	КонецЕсли;
	
	Для ВложенностьМакета = 1 По ИменаМакетов.Количество() - 1 Цикл
		ВложенныеСхемыКомпоновкиДанных = СхемаКомпоновкиДанных.ВложенныеСхемыКомпоновкиДанных;
		СхемаКомпоновкиДанных = ВложенныеСхемыКомпоновкиДанных.Найти(ИменаМакетов[ВложенностьМакета]).Схема;
	КонецЦикла;
	
	ПроверитьСхемуСКД(СхемаКомпоновкиДанных);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Настройки

Процедура ЗагрузитьНастройки()
	Если ЗначениеЗаполнено(Настройки) Тогда
		Возврат;
	КонецЕсли;

    ПлагинНастройки = КонтекстЯдра.Плагин("Настройки");
    ПлагинНастройки.Инициализация(КонтекстЯдра);
    
    Настройки = ПлагинНастройки.ПолучитьНастройку(КлючНастройки());
	
	НастройкиПоУмолчанию = НастройкиПоУмолчанию();
    Если ТипЗнч(Настройки) <> Тип("Структура") Then
        Настройки = НастройкиПоУмолчанию;
	Иначе
		ЗаполнитьЗначенияСвойств(НастройкиПоУмолчанию, Настройки);
        Настройки = НастройкиПоУмолчанию;
	КонецЕсли;
КонецПроцедуры

Функция НастройкиПоУмолчанию()
	
	Результат = Новый Структура;
	
	Результат.Вставить("Используется", Истина);
	Результат.Вставить("ИсключенияОбщихМакетов", Новый Массив);
	Результат.Вставить("ИсключенияПоИмениМетаданных", Новый Массив);
	Результат.Вставить("ИсключенияПоИмениМакетов", Новый Массив);
	
	Возврат Результат;
КонецФункции

Функция НужноВыполнятьТест()
	
	ЗагрузитьНастройки();
	
	Если Не ЗначениеЗаполнено(Настройки) Тогда
		Возврат Истина;
	КонецЕсли;
	
	КлючНастройки = КлючНастройки();
	
	ВыполнятьТест = Истина;
	Если ТипЗнч(Настройки) = Тип("Структура") 
		И Настройки.Свойство("Используется", ВыполнятьТест) Тогда

			Возврат ВыполнятьТест = Истина;	
	КонецЕсли;
	
	Возврат Истина;

КонецФункции

#КонецОбласти

Процедура ДобавитьОбщиеМакеты(НаборТестов)
	
	мНаборов = Новый Массив;
	
	Для Каждого ОбщийМакет Из Метаданные.ОбщиеМакеты Цикл
		
		Если ОбщийМакет.ТипМакета <> Метаданные.СвойстваОбъектов.ТипМакета.СхемаКомпоновкиДанных Тогда
			Продолжить;
		КонецЕсли;
		Сообщение = "Пропускаем из-за исключения по имени общего макета - " + 
			КонтекстЯдра.СтрШаблон_(ШаблонПредставления(), ТекстОбщиеМакеты(), ОбщийМакет.Имя);
		Если ДобавитьТестИсключениеЕслиЕстьВИсключаемойКоллекции(ОбщийМакет.Имя, Настройки.ИсключенияОбщихМакетов, 
					Сообщение, НаборТестов, мНаборов) Тогда
			Продолжить;
		КонецЕсли;
		
		мНаборов.Добавить(
			Новый Структура("ИмяПроцедуры, Параметры, Представление",
				"ТестДолжен_ПроверитьОбщийМакетСКД",
				НаборТестов.ПараметрыТеста(ОбщийМакет.Имя),
				КонтекстЯдра.СтрШаблон_(ШаблонПредставления(), ТекстОбщиеМакеты(), ОбщийМакет.Имя)));	
				
		ДобавитьВложенныеМакеты(мНаборов, ОбщийМакет, ОбщийМакет.Имя, ТекстОбщиеМакеты());
				
	КонецЦикла;
			
	Если мНаборов.Количество() > 0 Тогда
		
		НаборТестов.НачатьГруппу(ТекстОбщиеМакеты(), Ложь);
		
		Для Каждого Набор Из мНаборов Цикл
			
			НаборТестов.Добавить(Набор.ИмяПроцедуры, Набор.Параметры, Набор.Представление);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьМакетМетаданных(НаборТестов)
	
	ПроверяемыеОбъекты = ПроверяемыеМетаданные();
	
	Для Каждого ПроверяемыйОбъект Из ПроверяемыеОбъекты Цикл
		
		мНаборов = Новый Массив;
		
		Для Каждого ТекОбъект Из Метаданные[ПроверяемыйОбъект] Цикл
			
			ИмяМенеджера = ВРЕГ(ПроверяемыйОбъект);
			
			Сообщение = "Пропускаем из-за исключения по имени метаданного - " + 
				КонтекстЯдра.СтрШаблон_(ШаблонПредставления(), "Макеты", ТекОбъект.Имя);
			Если ДобавитьТестИсключениеЕслиЕстьВИсключаемойКоллекции(ТекОбъект.Имя, Настройки.ИсключенияПоИмениМетаданных, 
						Сообщение, НаборТестов, мНаборов) Тогда
				Продолжить;
			КонецЕсли;
			
			Для Каждого ТекДанныеМакета Из ТекОбъект.Макеты Цикл
				
				Если ТекДанныеМакета.ТипМакета <> Метаданные.СвойстваОбъектов.ТипМакета.СхемаКомпоновкиДанных Тогда
					Продолжить;
				КонецЕсли;

				Сообщение = "Пропускаем из-за исключения по имени макета - " + 
					КонтекстЯдра.СтрШаблон_(ШаблонПредставления(), ТекОбъект.Имя, ТекДанныеМакета.Имя);
				Если ДобавитьТестИсключениеЕслиЕстьВИсключаемойКоллекции(ТекДанныеМакета.Имя, Настройки.ИсключенияПоИмениМакетов, 
							Сообщение, НаборТестов, мНаборов) Тогда
					Продолжить;
				КонецЕсли;
				
				мНаборов.Добавить(
					Новый Структура("ИмяПроцедуры, Параметры, Представление",
						"ТестДолжен_ПроверитьМакетСКД",
						НаборТестов.ПараметрыТеста(ИмяМенеджера, ТекОбъект.Имя, ТекДанныеМакета.Имя),
						КонтекстЯдра.СтрШаблон_(ШаблонПредставления(), ТекОбъект.Имя, ТекДанныеМакета.Имя)));
											
				ДобавитьВложенныеМакеты(мНаборов, ТекДанныеМакета, ТекДанныеМакета.Имя, ТекОбъект.Имя, ИмяМенеджера);
						
			КонецЦикла;
			
		КонецЦикла;
		
		Если мНаборов.Количество() > 0 Тогда
			
			НаборТестов.НачатьГруппу(ПроверяемыйОбъект, Ложь);
			
			Для Каждого Набор Из мНаборов Цикл
				
				НаборТестов.Добавить(Набор.ИмяПроцедуры, Набор.Параметры, Набор.Представление);
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьВложенныеМакеты(МассивТестов, Родитель, ИмяРодителя, ИмяОбъекта = "", ИмяМенеджера = "")
	
	Если ТипЗнч(Родитель) = Тип("ВложеннаяСхемаКомпоновкиДанных") Тогда
		Макет = Родитель.Схема;
	ИначеЕсли ЗначениеЗаполнено(ИмяМенеджера) Тогда
		Менеджер = МенеджерОбьектаПоИмени(ВРег(ИмяМенеджера));
		Макет = Менеджер[ИмяОбъекта].ПолучитьМакет(СокрЛП(Родитель.Имя));
	Иначе
		Макет = ПолучитьОбщийМакет(Родитель.Имя);
	КонецЕсли;
	
	Для Каждого ВложенныйМакет Из Макет.ВложенныеСхемыКомпоновкиДанных Цикл
		
		ИмяМакета = КонтекстЯдра.СтрШаблон_("%1->%2", ИмяРодителя, ВложенныйМакет.Имя);	
		Представление = КонтекстЯдра.СтрШаблон_(ШаблонПредставления(), ИмяОбъекта, ИмяМакета);
		
		ПараметрыТеста = Новый Массив;
		ПараметрыТеста.Добавить(ИмяМенеджера);
		ПараметрыТеста.Добавить(ИмяОбъекта);
		ПараметрыТеста.Добавить(ИмяМакета);
		
		СтруктураТеста = Новый Структура;
		СтруктураТеста.Вставить("ИмяПроцедуры", "ТестДолжен_ПроверитьВложенныйМакетСКД");
		СтруктураТеста.Вставить("Параметры", ПараметрыТеста);
		СтруктураТеста.Вставить("Представление", Представление);
		МассивТестов.Добавить(СтруктураТеста);			
		
		ДобавитьВложенныеМакеты(МассивТестов, ВложенныйМакет, ИмяМакета, ИмяОбъекта, ИмяМенеджера);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьСхемуСКД(СхемаКомпоновкиДанных)

	ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных;
	
	КомпоновщикНастроекКомпоновкиДанных = Новый КомпоновщикНастроекКомпоновкиДанных;
	ИсточникДоступныхНастроекКомпоновкиДанных = Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных);
	
	// Тут проходит синтаксический анализ запроса.
    КомпоновщикНастроекКомпоновкиДанных.Инициализировать(ИсточникДоступныхНастроекКомпоновкиДанных);
	
КонецПроцедуры

Функция ПроверяемыеМетаданные()
	
	ПроверяемыеОбъекты = Новый Массив();
	ПроверяемыеОбъекты.Добавить("Справочники");
	ПроверяемыеОбъекты.Добавить("Документы");
	ПроверяемыеОбъекты.Добавить("Обработки");
	ПроверяемыеОбъекты.Добавить("Отчеты");
	ПроверяемыеОбъекты.Добавить("Перечисления");
	ПроверяемыеОбъекты.Добавить("ПланыВидовХарактеристик");
	ПроверяемыеОбъекты.Добавить("ПланыСчетов");
	ПроверяемыеОбъекты.Добавить("ПланыВидовРасчета");
	ПроверяемыеОбъекты.Добавить("РегистрыСведений");
	ПроверяемыеОбъекты.Добавить("РегистрыНакопления");
	ПроверяемыеОбъекты.Добавить("РегистрыБухгалтерии");
	ПроверяемыеОбъекты.Добавить("РегистрыРасчета");
	ПроверяемыеОбъекты.Добавить("БизнесПроцессы");
	ПроверяемыеОбъекты.Добавить("Задачи");
	
	Возврат ПроверяемыеОбъекты;
	
КонецФункции

Функция МенеджерОбьектаПоИмени(Знач ИмяМенеджера)
	
	Попытка
	    Менеджер = Вычислить(ИмяМенеджера);
	Исключение
		Менеджер = Неопределено;	
	КонецПопытки;
		
	Возврат Менеджер;
	
КонецФункции

Функция ШаблонПредставления()
	Возврат НСтр("ru = 'Валидация корректности запроса СКД в %1: %2'");
КонецФункции

Функция ТекстОбщиеМакеты()
	Возврат "ОбщиеМакеты";
КонецФункции

Функция ДобавитьТестИсключениеЕслиЕстьВИсключаемойКоллекции(Знач ЧтоИщем, Знач КоллекцияДляПоиска, Знач Сообщение, 
			Знач НаборТестов, Знач Наборы)
			
	Если КонтекстЯдра.ЕстьВИсключаемойКоллекции(ЧтоИщем, КоллекцияДляПоиска) Тогда
		КонтекстЯдра.Отладка(Сообщение);
		ПараметрыТеста = НаборТестов.ПараметрыТеста(Сообщение);
		
		//НаборТестов.Добавить("Тест_ПропуститьПечатнуюФорму", ПараметрыТеста, Сообщение);
		Наборы.Добавить(
			Новый Структура("ИмяПроцедуры, Параметры, Представление",
				"Тест_ПропуститьПечатнуюФорму",
				ПараметрыТеста,
				Сообщение));	
		
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
КонецФункции

#КонецОбласти
